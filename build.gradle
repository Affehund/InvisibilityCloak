subprojects {

    apply plugin: 'java'
    java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)

    jar {
        manifest {
            attributes([
                    'Specification-Title'     : mod_name,
                    'Specification-Vendor'    : mod_author,
                    'Specification-Version'   : project.jar.archiveVersion,
                    'Implementation-Title'    : project.name,
                    'Implementation-Version'  : project.jar.archiveVersion,
                    'Implementation-Vendor'   : mod_author,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                    'Build-On-Minecraft'      : minecraft_version,
                    "MixinConfigs"            : mod_id + ".mixins.json"
            ])
        }
    }

    repositories {
        mavenCentral()

        maven {
            name = 'Sponge'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }

        maven {
            name = "TerraformersMC"
            url = "https://maven.terraformersmc.com/"
        }
        
        maven {
            name = "Ladysnake Libs"
            url = "https://ladysnake.jfrog.io/artifactory/mods"
        }

        maven {
            name = "shedaniel"
            url = "https://maven.shedaniel.me/"
        }

        maven {
            name = "TheIllusiveC4"
            url = "https://maven.theillusivec4.top/"
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = java_version as int
    }
}